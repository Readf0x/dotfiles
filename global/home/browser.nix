{ pkgs, lib, ... }: {
  textfox = {
    enable = true;
    profile = "Default";
  };
  programs.firefox = {
    enable = true;
    package = pkgs.firefox-esr;
    nativeMessagingHosts = [ pkgs.keepassxc ];

    policies = {
      DisableAppUpdate = true;
      AppUpdateURL = "https://localhost/";
      OverrideFirstRunPage = "";
      OverridePostUpdatePage = "";
      DisableFirefoxStudies = true;
      DisableTelemetry = true;
      DisableFeedbackCommands = true;
      DisablePocket = true;
      DisableFirefoxAccounts = true;
      DisableFirefoxScreenshots = true;
      DisableBuiltinPDFViewer = true;
      DisableSetDesktopBackground = true;
      NoDefaultBookmarks = false;
      Extensions.Uninstall = [
        "google@search.mozilla.org"
        "bing@search.mozilla.org"
        "amazondotcom@search.mozilla.org"
        "ebay@search.mozilla.org"
        "twitter@search.mozilla.org"
      ];
      # Apparently only supported in the ESR so that's great
      SearchEngines = {
        Remove = [
          "google@search.mozilla.org"
          "bing@search.mozilla.org"
          "amazondotcom@search.mozilla.org"
          "ebay@search.mozilla.org"
          "twitter@search.mozilla.org"
        ];
        Default = "DuckDuckGo";
      };
      ExtensionSettings = let
        latest = addon_id: "https://addons.mozilla.org/firefox/downloads/latest/${addon_id}/latest.xpi";
        mapExt = builtins.mapAttrs (name: value: {
          installation_mode = "normal_installed";
          install_url = value;
        });
      in mapExt {
        "{36bdf805-c6f2-4f41-94d2-9b646342c1dc}" = latest "export-cookies-txt";
        "{74145f27-f039-47ce-a470-a662b129930a}" = latest "clearurls";
        "{b86e4813-687a-43e6-ab65-0bde4ab75758}" = latest "localcdn-fork-of-decentraleyes";
        "DontFuckWithPaste@raim.ist" = latest "don-t-fuck-with-paste";
        "{531906d3-e22f-4a6c-a102-8057b88a1a63}" = latest "single-file";
        "skipredirect@sblask" = latest "skip-redirect";
        "7esoorv3@alefvanoon.anonaddy.me" = latest "libredirect";
        "gdpr@cavi.au.dk" = latest "consent_o_matic";
        "keepassxc-browser@keepassxc.org" = latest "keepassxc-browser";
        "mouse-pinch-to-zoom@niziolek.dev" = latest "mouse-pinch-to-zoom";
        "new-window-without-toolbar@tkrkt.com" = latest "new-window-without-toolbar";
        "jid1-93WyvpgvxzGATw@jetpack" = latest "to-google-translate";
        # "tubemod@extension.com" = latest "tubemod";
        "{d7742d87-e61d-4b78-b8a1-b469842139fa}" = latest "vimium-ff";
      } // {
        "uBlock0@raymondhill.net" = {
          installation_mode = "force_installed";
          install_url = latest "ublock-origin";
        };
      };
      SupportMenu = {
        Title = "Submit an issue"; # LMAO who is ever gonna click on this??
        URL = "https://github.com/readf0x/dotfiles/issues";
      };
    };

    # [TODO] Decide whether or not to save history & tabs
    profiles = {
      Default = {
        id = 0;
        isDefault = true;
        # This file was generated by Chat Gippity, don't trust it
        # I have reviewed it, but not *thoroughly*
        settings = import ./firefox_hardening_config.nix // {
          "middlemouse.paste" = false;
          "toolkit.legacyUserProfileCustomizations.stylesheets" = true;
          # It's pretty cool but it's broken on 24.11, untested with ESR
          # "widget.use-xdg-desktop-portal.file-picker" = 1;
          "widget.use-xdg-desktop-portal.mime-handler" = 1;
          "browser.toolbars.bookmarks.visibility" = "never";
          "extensions.autoDisableScopes" = 0;
          "browser.display.background_color" = "#282828";
          "privacy.fingerprintingProtection" = true;
          "privacy.fingerprintingProtection.overrides" = "+AllTargets,-CSSPrefersColorScheme";
          "extensions.webextensions.ExtensionStorageIDB.enabled" = false;
        };

        bookmarks = [
          {
            name = "NixOS Packages";
            url = "https://search.nixos.org/packages";
          }
          {
            name = "Nix Manual";
            url = "https://ryantm.github.io/nixpkgs/";
          }
          {
            name = "Immich";
            url = "http://immich.planetbob.net/";
          }
          {
            name = "Requests";
            url = "http://requests.planetbob.net/";
          }
          {
            name = "AMP";
            url = "http://10.0.0.2:8081/";
          }
          {
            name = "Git";
            url = "http://gitlab.planetbob.net/";
          }
          {
            name = "LAINCHAN";
            url = "https://lainchan.org/";
          }
          {
            name = "Quickshell";
            url = "https://quickshell.outfoxxed.me/";
          }
        ];

        extensions = with pkgs.nur.repos.rycee.firefox-addons; [
          consent-o-matic
          darkreader
          enhancer-for-youtube
          firefox-color
          privacy-badger
          purpleadblock
          reddit-enhancement-suite
          return-youtube-dislikes
          seventv
          sponsorblock
          twitch-auto-points

          (buildFirefoxXpiAddon {
            pname = "twitch-live-channels";
            version = "1.0.24";
            addonId = "{d3d2a327-1ae0-4fd6-b732-0844d0b7fd4c}";
            url = "https://addons.mozilla.org/firefox/downloads/latest/twitch-live-channels/latest.xpi";
            sha256 = "sha256-AZqaXyX6rHB3ZABQUbMx3AESAaWIjaCO9/301nV+RSo=";
            meta = { homepage = "https://github.com/s4my/TwitchLiveChannels/";
              description = "Twitch Live Channels helps you keep track of who is LIVE out of the channels you follow on Twitch.";
              license = lib.licenses.gpl3;
              mozPermissions = [
                "storage"
                "notifications"
                "identity"
                "https://*.twitch.tv/"
              ];
              platforms = lib.platforms.all;
            };
          })
        ];

        search = {
          default = "DuckDuckGo";
          engines = {};
          force = true;
        };
      };
      I2P = {
        id = 1;
        isDefault = false;
      };
    };
  };
}
